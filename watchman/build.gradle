

apply plugin: "java"
apply plugin: "scala"
//apply plugin: "play"
apply plugin: "eclipse"


jar {
	baseName = 'kumori-watchman'
	version =  '0.1.0'
	
	 manifest {
        attributes "Main-Class": "dk.mathmagicians.watchman.Main"
    }
    
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}


repositories {
	jcenter()
	 maven {
        name "lightbend-maven-release"
        url "https://repo.lightbend.com/lightbend/maven-releases"
    }
    ivy {
        name "lightbend-ivy-release"
        url "https://repo.lightbend.com/lightbend/ivy-releases"
        layout "ivy"
    }
}

//JVM settings
sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

// eclipse fucks up :( - comment out wrapper task on import
task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
}

//kudos to https://blog.knoldus.com/2017/08/20/scalagradlecucumber-a-holy-alliance/
task cucumber(type: JavaExec) {
	description "Executes the cucumber tests"
    main = "cucumber.api.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = ['--plugin', 'pretty', '--glue', 'dk.mathmagicians', 'src/test/resources']
}

task integrationTest(type: Test) {
	group 'verification'
	description "Executes the integration test features using CucumberJVM for Scala"
	dependsOn cucumber
	outputs.upToDateWhen { false }
}

// Ensure that our integration tests are run before the check task and that the check task fails the build if there are failing integration tests.
// Ensure that our unit tests are run before our integration tests. This guarantees that our unit tests are run even if our integration tests fails.
check.dependsOn integrationTest
integrationTest.mustRunAfter test

// kudos to: https://stackoverflow.com/questions/28387045/how-to-run-the-cucumber-test-using-gradle
// Ensure that the HTML reports of unit and integration tests are created to different report
// build/reports/integrationTest directory contains the HTML report that contains the test results of our integration tests.
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}


dependencies {
    compile 'org.scala-lang:scala-library:2.12.4'
    compile 'com.typesafe:config:1.3.3'
    compile 'io.cucumber:cucumber-scala_2.12:2.0.1'
    compile 'org.scalactic:scalactic_2.12:3.0.5'
    testCompile 'io.cucumber:cucumber-core:2.0.1'
    testCompile 'io.cucumber:cucumber-junit:2.0.1'
   
    testCompile 'junit:junit:4.12'
  //  testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
}
